// This block specifies the dependencies needed to run rest of the script.
buildscript {
    ext {
        gaeVer = '1.9.34'
        gwtVer = '2.7.0'
    }

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath "com.google.appengine:gradle-appengine-plugin:$gaeVer"
        classpath "de.richsource.gradle.plugins:gwt-gradle-plugin:0.6"
    }
}

repositories {
    mavenCentral()
}

apply plugin: "java"
apply plugin: "war"
apply plugin: "gwt"
apply plugin: "appengine"

// App Engine standard environment needs Java 7.
sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

dependencies {
    // Include App Engine as a dependency.
    compile "com.google.appengine:appengine-api-1.0-sdk:$gaeVer"

    // Specifies the App Engine SDK to download and use.
    appengineSdk "com.google.appengine:appengine-java-sdk:$gaeVer"

    // Include GWT material
    compile "com.github.gwtmaterialdesign:gwt-material:1.5.0"

    // Dependencies for development. `providedCompile` dependencies
    // are not included in the output build. These are only included
    // so Eclipse can properly resolve classpaths.
    // providedCompile "com.google.gwt:gwt-user:$gwtVer"
    // providedCompile "com.google.gwt:gwt-dev:$gwtVer"
}

compileJava {
    options.incremental = true
}

gwt {
    // Specifying a version number allows GWT plugin to inject the proper
    // dependencies during build.
    gwtVersion = gwtVer

    // This points to src/main/java/com/jingyu/example/Example.gwt.xml
    modules "com.jingyu.example.Example"

    // Hungry hungry hippo
    minHeapSize = "512M"
    maxHeapSize = "1024M"
}

appengine {
    // We will let Gradle download and manage the App Engine SDK for us.
    downloadSdk = true

    // Uncomment the follow line to enable debugging of the App Engine
    // jvmFlags = ['-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=8000']

    appcfg {
        // The future.
        oauth2 = true
    }
}