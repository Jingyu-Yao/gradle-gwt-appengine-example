// This block specifies the dependencies needed to run rest of the script.
buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath "com.google.appengine:gradle-appengine-plugin:1.9.34"
        classpath "de.richsource.gradle.plugins:gwt-gradle-plugin:0.6"
    }
}

repositories {
    mavenCentral()
}

apply plugin: "java"
apply plugin: "war"
apply plugin: "gwt"
apply plugin: "appengine"

// App Engine standard environment needs Java 7.
sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

dependencies {
    // Include App Engine as a dependency.
    compile "com.google.appengine:appengine-api-1.0-sdk:1.9.34"

    // Specifies the App Engine SDK to download and use.
    appengineSdk "com.google.appengine:appengine-java-sdk:1.9.34"

    // Dependencies for development. `providedCompile` dependencies
    // are not included in the output build. These are only included
    // so Eclipse can properly resolve classpaths.
    providedCompile "com.google.gwt:gwt-user:2.7.0"
    providedCompile "com.google.gwt:gwt-dev:2.7.0"
}

gwt {
    // Specifying a version number allows GWT plugin to inject the proper
    // dependencies during build.
    gwtVersion = "2.7.0"

    // This points to src/main/java/com/jingyu/example/Example.gwt.xml
    modules "com.jingyu.example.Example"
}

appengine {
    // We will let Gradle download and manage the App Engine SDK for us.
    downloadSdk = true

    // Uncomment the follow line to enable debugging of the App Engine
    // jvmFlags = ['-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=8000']

    appcfg {
        // The future.
        oauth2 = true
    }
}